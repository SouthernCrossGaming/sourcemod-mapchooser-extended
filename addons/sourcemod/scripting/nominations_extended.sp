/**
 * vim: set ts=4 :
 * =============================================================================
 * Nominations Extended
 * Allows players to nominate maps for Mapchooser
 *
 * Nominations Extended (C)2012-2013 Powerlord (Ross Bemrose)
 * SourceMod (C)2004-2007 AlliedModders LLC.  All rights reserved.
 * =============================================================================
 *
 * This program is free software; you can redistribute it and/or modify it under
 * the terms of the GNU General Public License, version 3.0, as published by the
 * Free Software Foundation.
 *
 * This program is distributed in the hope that it will be useful, but WITHOUT
 * ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS
 * FOR A PARTICULAR PURPOSE.  See the GNU General Public License for more
 * details.
 *
 * You should have received a copy of the GNU General Public License along with
 * this program.  If not, see <http://www.gnu.org/licenses/>.
 *
 * As a special exception, AlliedModders LLC gives you permission to link the
 * code of this program (as well as its derivative works) to "Half-Life 2," the
 * "Source Engine," the "SourcePawn JIT," and any Game MODs that run on software
 * by the Valve Corporation.  You must obey the GNU General Public License in
 * all respects for all other code used.  Additionally, AlliedModders LLC grants
 * this exception to all derivative works.  AlliedModders LLC defines further
 * exceptions, found in LICENSE.txt (as of this writing, version JULY-31-2007),
 * or <http://www.sourcemod.net/license.php>.
 *
 * Version: $Id$
 */

#include <sourcemod>
#include <mapchooser>
#include "include/mapchooser_extended"
#include <morecolors>
#pragma semicolon 1
#pragma newdecls required

#define MCE_VERSION "1.10.0"

public Plugin myinfo =
{
	name = "Map Nominations Extended",
	author = "Powerlord and AlliedModders LLC",
	description = "Provides Map Nominations",
	version = MCE_VERSION,
	url = "https://forums.alliedmods.net/showthread.php?t=156974"
};

Handle g_Cvar_ExcludeOld = INVALID_HANDLE;
Handle g_Cvar_ExcludeCurrent = INVALID_HANDLE;
Handle g_Cvar_ExcludeModes = INVALID_HANDLE;
Handle g_Cvar_DisplayName = INVALID_HANDLE;

Handle g_MapList = INVALID_HANDLE;
Handle g_MapMenu = INVALID_HANDLE;
int g_mapFileSerial = -1;

#define MAPSTATUS_ENABLED (1<<0)
#define MAPSTATUS_DISABLED (1<<1)
#define MAPSTATUS_EXCLUDE_CURRENT (1<<2)
#define MAPSTATUS_EXCLUDE_PREVIOUS (1<<3)
#define MAPSTATUS_EXCLUDE_NOMINATED (1<<4)
#define MAPSTATUS_EXCLUDE_PREVIOUS_GAMEMODE (1<<5)

Handle g_mapTrie;

// Nominations Extended Convars
Handle g_Cvar_MarkCustomMaps = INVALID_HANDLE;

public void OnPluginStart()
{
	LoadTranslations("common.phrases");
	LoadTranslations("nominations.phrases");
	LoadTranslations("basetriggers.phrases"); // for Next Map phrase
	LoadTranslations("mapchooser_extended.phrases");

	int arraySize = ByteCountToCells(PLATFORM_MAX_PATH);
	g_MapList = CreateArray(arraySize);

	g_Cvar_ExcludeOld = CreateConVar("sm_nominate_excludeold", "1", "Specifies if the current map should be excluded from the Nominations list", 0, true, 0.00, true, 1.0);
	g_Cvar_ExcludeCurrent = CreateConVar("sm_nominate_excludecurrent", "1", "Specifies if the MapChooser excluded maps should also be excluded from Nominations", 0, true, 0.00, true, 1.0);
	g_Cvar_ExcludeModes = CreateConVar("sm_nominate_excludemode", "1", "Specifies if the current mode should be excluded from the Nominations list", 0, true, 0.00, true, 1.0);
	g_Cvar_DisplayName = CreateConVar("sm_nominate_displayname", "1", "Use custom Display Names instead of the raw map name", 0, true, 0.00, true, 1.0);

	RegConsoleCmd("say", Command_Say);
	RegConsoleCmd("say_team", Command_Say);

	RegConsoleCmd("sm_nominate", Command_Nominate);

	RegAdminCmd("sm_nominate_addmap", Command_Addmap, ADMFLAG_CHANGEMAP, "sm_nominate_addmap <mapname> - Forces a map to be on the next mapvote.");

	// Nominations Extended cvars
	CreateConVar("ne_version", MCE_VERSION, "Nominations Extended Version", FCVAR_SPONLY|FCVAR_NOTIFY|FCVAR_DONTRECORD);


	g_mapTrie = CreateTrie();
}

public void OnAllPluginsLoaded()
{
	// This is an MCE cvar... this plugin requires MCE to be loaded.  Granted, this plugin SHOULD have an MCE dependency.
	g_Cvar_MarkCustomMaps = FindConVar("mce_markcustommaps");
}

public void OnConfigsExecuted()
{
	if (ReadMapList(g_MapList,
					g_mapFileSerial,
					"nominations",
					MAPLIST_FLAG_CLEARARRAY|MAPLIST_FLAG_MAPSFOLDER)
		== INVALID_HANDLE)
	{
		if (g_mapFileSerial == -1)
		{
			SetFailState("Unable to create a valid map list.");
		}
	}

	BuildMapMenu();
}

public void OnNominationRemoved(const char[] map, int owner)
{
	int status;

	/* Is the map in our list? */
	if (!GetTrieValue(g_mapTrie, map, status))
	{
		return;
	}

	/* Was the map disabled due to being nominated */
	if ((status & MAPSTATUS_EXCLUDE_NOMINATED) != MAPSTATUS_EXCLUDE_NOMINATED)
	{
		return;
	}

	SetTrieValue(g_mapTrie, map, MAPSTATUS_ENABLED);
}

stock void getMapName(const char[] map, char[] mapName, int size)
{
	if (GetConVarBool(g_Cvar_DisplayName))
	{
		GetMapName(map, mapName, size);
		return;
	}
	strcopy(mapName, size, map);
}

public Action Command_Addmap(int client, int args)
{
	if (args < 1)
	{
		CReplyToCommand(client, "[NE] Usage: sm_nominate_addmap <mapname>");
		return Plugin_Handled;
	}

	char mapname[PLATFORM_MAX_PATH];
	GetCmdArg(1, mapname, sizeof(mapname));

	char mapName[PLATFORM_MAX_PATH];
	getMapName(mapname, mapName, sizeof(mapName));

	int status;
	if (!GetTrieValue(g_mapTrie, mapname, status))
	{
		CReplyToCommand(client, "%t", "Map was not found", mapName);
		return Plugin_Handled;
	}

	NominateResult result = NominateMap(mapname, true, 0);

	if (result > Nominate_Replaced)
	{
		/* We assume already in vote is the casue because the maplist does a Map Validity check and we forced, so it can't be full */
		CReplyToCommand(client, "%t", "Map Already In Vote", mapName);

		return Plugin_Handled;
	}


	SetTrieValue(g_mapTrie, mapname, MAPSTATUS_DISABLED|MAPSTATUS_EXCLUDE_NOMINATED);


	CReplyToCommand(client, "%t", "Map Inserted", mapName);
	LogAction(client, -1, "\"%L\" inserted map \"%s\".", client, mapname);

	return Plugin_Handled;
}

public Action Command_Say(int client, int args)
{
	if (!client)
	{
		return Plugin_Continue;
	}

	char text[192];
	if (!GetCmdArgString(text, sizeof(text)))
	{
		return Plugin_Continue;
	}

	int startidx = 0;
	if(text[strlen(text)-1] == '"')
	{
		text[strlen(text)-1] = '\0';
		startidx = 1;
	}

	ReplySource old = SetCmdReplySource(SM_REPLY_TO_CHAT);

	if (strcmp(text[startidx], "nominate", false) == 0)
	{
		if (IsNominateAllowed(client))
		{
			AttemptNominate(client);
		}
	}

	SetCmdReplySource(old);

	return Plugin_Continue;
}

public Action Command_Nominate(int client, int args)
{
	if (!client || !IsNominateAllowed(client))
	{
		return Plugin_Handled;
	}

	//Temporary fix

	AttemptNominate(client);
	return Plugin_Handled;

	// if (args == 0)
	// {
	// 	AttemptNominate(client);
	// 	return Plugin_Handled;
	// }

	// char mapname[PLATFORM_MAX_PATH];
	// char resolvedMap[PLATFORM_MAX_PATH];
	// GetCmdArg(1, mapname, sizeof(mapname));

	// if (FindMap(mapname, resolvedMap, sizeof(resolvedMap)) == FindMap_NotFound)
	// {
	// 	// We couldn't resolve the map entry to a filename, so...
	// 	CReplyToCommand(client, "%t", "Map was not found", mapname);
	// 	return Plugin_Handled;		
	// }

	// int status;
	// if (!GetTrieValue(g_mapTrie, resolvedMap, status))
	// {
	// 	CReplyToCommand(client, "%t (%s)", "Map was not found", mapname, resolvedMap);
	// 	return Plugin_Handled;
	// }

	// strcopy(mapname, sizeof(mapname), resolvedMap);

	// if ((status & MAPSTATUS_DISABLED) == MAPSTATUS_DISABLED)
	// {
	// 	if ((status & MAPSTATUS_EXCLUDE_CURRENT) == MAPSTATUS_EXCLUDE_CURRENT)
	// 	{
	// 		CReplyToCommand(client, "[NE] %t", "Can't Nominate Current Map");
	// 	}

	// 	if ((status & MAPSTATUS_EXCLUDE_PREVIOUS) == MAPSTATUS_EXCLUDE_PREVIOUS)
	// 	{
	// 		CReplyToCommand(client, "[NE] %t", "Map in Exclude List");
	// 	}

	// 	if ((status & MAPSTATUS_EXCLUDE_NOMINATED) == MAPSTATUS_EXCLUDE_NOMINATED)
	// 	{
	// 		CReplyToCommand(client, "[NE] %t", "Map Already Nominated");
	// 	}

	// 	if ((status & MAPSTATUS_EXCLUDE_PREVIOUS_GAMEMODE) == MAPSTATUS_EXCLUDE_PREVIOUS_GAMEMODE)
	// 	{
	// 		CReplyToCommand(client, "[NE] %t (%s)", "Map is Similar Game Mode", mapname);
	// 	}

	// 	return Plugin_Handled;
	// }

	// NominateResult result = NominateMap(mapname, false, client);

	// if (result > Nominate_Replaced)
	// {
	// 	if (result == Nominate_AlreadyInVote)
	// 	{
	// 		CReplyToCommand(client, "%t", "Map Already In Vote", mapname);
	// 	}
	// 	else
	// 	{
	// 		CReplyToCommand(client, "[NE] %t", "Map Already Nominated");
	// 	}

	// 	return Plugin_Handled;
	// }

	// /* Map was nominated! - Disable the menu item and update the trie */

	// SetTrieValue(g_mapTrie, mapname, MAPSTATUS_DISABLED|MAPSTATUS_EXCLUDE_NOMINATED);

	// char mapName[PLATFORM_MAX_PATH];
	// getMapName(mapname, mapName, sizeof(mapName));
	// char name[MAX_NAME_LENGTH];
	// GetClientName(client, name, sizeof(name));
	// PrintToChatAll("[NE] %t", "Map Nominated", name, mapName);
	// LogMessage("%s nominated %s", name, mapname);

	// return Plugin_Continue;
}

void AttemptNominate(int client)
{
	SetMenuTitle(g_MapMenu, "%T", "Nominate Title", client);
	DisplayMenu(g_MapMenu, client, MENU_TIME_FOREVER);

	return;
}

void BuildMapMenu()
{
	if (g_MapMenu != INVALID_HANDLE)
	{
		CloseHandle(g_MapMenu);
		g_MapMenu = INVALID_HANDLE;
	}

	ClearTrie(g_mapTrie);

	g_MapMenu = CreateMenu(Handler_MapSelectMenu, MENU_ACTIONS_DEFAULT|MenuAction_DrawItem|MenuAction_DisplayItem);

	char map[PLATFORM_MAX_PATH];

	ArrayList excludeMaps;
	excludeMaps = new ArrayList(ByteCountToCells(PLATFORM_MAX_PATH));

	ArrayList excludeGameModeMaps;
	excludeGameModeMaps = new ArrayList(ByteCountToCells(PLATFORM_MAX_PATH));

	char currentMap[32];

	if (GetConVarBool(g_Cvar_ExcludeOld))
	{
		GetExcludeMapList(excludeMaps);
	}
	if (GetConVarBool(g_Cvar_ExcludeModes))
	{
		GetExcludeGameModeMapList(excludeGameModeMaps);
	}

	if (GetConVarBool(g_Cvar_ExcludeCurrent))
	{
		GetCurrentMap(currentMap, sizeof(currentMap));
	}

	bool DisplayName = GetConVarBool(g_Cvar_DisplayName);

	for (int i = 0; i < GetArraySize(g_MapList); i++)
	{
		int status = MAPSTATUS_ENABLED;

		GetArrayString(g_MapList, i, map, sizeof(map));

		if (GetConVarBool(g_Cvar_ExcludeCurrent))
		{
			if (StrEqual(map, currentMap))
			{
				status = MAPSTATUS_DISABLED|MAPSTATUS_EXCLUDE_CURRENT;
			}
		}

		/* Dont bother with this check if the current map check failed */
		if (GetConVarBool(g_Cvar_ExcludeOld) && status == MAPSTATUS_ENABLED)
		{
			if (FindStringInArray(excludeMaps, map) != -1)
			{
				status = MAPSTATUS_DISABLED|MAPSTATUS_EXCLUDE_PREVIOUS;
			}
		}

		/* Dont bother with this check if the old map check failed */
		if (GetConVarBool(g_Cvar_ExcludeModes) && status == MAPSTATUS_ENABLED)
		{
			if (FindStringInArray(excludeGameModeMaps, map) != -1)
			{
				status = MAPSTATUS_DISABLED|MAPSTATUS_EXCLUDE_PREVIOUS_GAMEMODE;
			}
		}

		//AddMenuItem(g_MapMenu, map, map);
		if (DisplayName)
		{
			char mapName[PLATFORM_MAX_PATH];
			GetMapName(map, mapName, sizeof(mapName));
			AddMenuItem(g_MapMenu, map, mapName);
		}
		else
		{
			AddMenuItem(g_MapMenu, map, map);
		}
		SetTrieValue(g_mapTrie, map, status);
	}

	SetMenuExitButton(g_MapMenu, true);

	if (excludeMaps != INVALID_HANDLE)
	{
		CloseHandle(excludeMaps);
	}
}

public int Handler_MapSelectMenu(Handle menu, MenuAction action, int param1, int param2)
{
	switch (action)
	{
		case MenuAction_Select:
		{
			char map[PLATFORM_MAX_PATH], name[MAX_NAME_LENGTH];
			GetMenuItem(menu, param2, map, sizeof(map));

			char mapName[PLATFORM_MAX_PATH];
			getMapName(map, mapName, sizeof(mapName));

			GetClientName(param1, name, MAX_NAME_LENGTH);

			NominateResult result = NominateMap(map, false, param1);

			/* Don't need to check for InvalidMap because the menu did that already */
			if (result == Nominate_AlreadyInVote)
			{
				PrintToChat(param1, "[NE] %t", "Map Already Nominated");
				return 0;
			}
			else if (result == Nominate_VoteFull)
			{
				PrintToChat(param1, "[NE] %t", "Max Nominations");
				return 0;
			}

			SetTrieValue(g_mapTrie, map, MAPSTATUS_DISABLED|MAPSTATUS_EXCLUDE_NOMINATED);

			if (result == Nominate_Replaced)
			{
				PrintToChatAll("[NE] %t", "Map Nomination Changed", name, mapName);
				return 0;
			}

			PrintToChatAll("[NE] %t", "Map Nominated", name, mapName);
			LogMessage("%s nominated %s", name, map);
		}

		case MenuAction_DrawItem:
		{
			char map[PLATFORM_MAX_PATH];
			GetMenuItem(menu, param2, map, sizeof(map));

			int status;

			if (!GetTrieValue(g_mapTrie, map, status))
			{
				LogError("Menu selection of item not in trie. Major logic problem somewhere.");
				return ITEMDRAW_DEFAULT;
			}

			if ((status & MAPSTATUS_DISABLED) == MAPSTATUS_DISABLED)
			{
				return ITEMDRAW_DISABLED;
			}

			return ITEMDRAW_DEFAULT;

		}

		case MenuAction_DisplayItem:
		{
			char map[PLATFORM_MAX_PATH];
			GetMenuItem(menu, param2, map, sizeof(map));

			int mark = GetConVarInt(g_Cvar_MarkCustomMaps);
			bool official;

			int status;

			if (!GetTrieValue(g_mapTrie, map, status))
			{
				LogError("Menu selection of item not in trie. Major logic problem somewhere.");
				return 0;
			}

			char buffer[100];
			char display[150];

			if (mark)
			{
				official = IsMapOfficial(map);
			}

			if (mark && !official)
			{
				char bufferT[100];
				getMapName(map, bufferT, sizeof(bufferT));
				switch (mark)
				{
					case 1:
					{
						Format(buffer, sizeof(buffer), "%T", "Custom Marked", param1, bufferT);
					}

					case 2:
					{
						Format(buffer, sizeof(buffer), "%T", "Custom", param1, bufferT);
					}
				}
			}
			else
			{
				//strcopy(buffer, sizeof(buffer), map);
				getMapName(map, buffer, sizeof(buffer));
			}

			if ((status & MAPSTATUS_DISABLED) == MAPSTATUS_DISABLED)
			{
				if ((status & MAPSTATUS_EXCLUDE_CURRENT) == MAPSTATUS_EXCLUDE_CURRENT)
				{
					Format(display, sizeof(display), "%s (%T)", buffer, "Current Map", param1);
					return RedrawMenuItem(display);
				}

				if ((status & MAPSTATUS_EXCLUDE_PREVIOUS) == MAPSTATUS_EXCLUDE_PREVIOUS)
				{
					Format(display, sizeof(display), "%s (%T)", buffer, "Recently Played", param1);
					return RedrawMenuItem(display);
				}

				if ((status & MAPSTATUS_EXCLUDE_NOMINATED) == MAPSTATUS_EXCLUDE_NOMINATED)
				{
					Format(display, sizeof(display), "%s (%T)", buffer, "Nominated", param1);
					return RedrawMenuItem(display);
				}

				if ((status & MAPSTATUS_EXCLUDE_PREVIOUS_GAMEMODE) == MAPSTATUS_EXCLUDE_PREVIOUS_GAMEMODE)
				{
					Format(display, sizeof(display), "%s (%T)", buffer, "Similar Game Mode", param1);
					return RedrawMenuItem(display);
				}
			}

			if (mark && !official)
				return RedrawMenuItem(buffer);

			return 0;
		}
	}

	return 0;
}

stock bool IsNominateAllowed(int client)
{
	CanNominateResult result = CanNominate();

	switch(result)
	{
		case CanNominate_No_VoteInProgress:
		{
			CReplyToCommand(client, "[NE] %t", "Nextmap Voting Started");
			return false;
		}

		case CanNominate_No_VoteComplete:
		{
			char map[PLATFORM_MAX_PATH];
			GetNextMap(map, sizeof(map));
			CReplyToCommand(client, "[NE] %t", "Next Map", map);
			return false;
		}

		case CanNominate_No_VoteFull:
		{
			CReplyToCommand(client, "[NE] %t", "Max Nominations");
			return false;
		}
	}

	return true;
}